REBOL [
    Title:   "EAN-13 Barcode Image Generator"
    Date:    19-May-2005
    Name:    'EAN13  ; For window title bar

    Version: 0.1.3
    File:    %ean13.r
    Home:    http://www.hmkdesign.dk/rebol/ean13.r

    Author:  "Henrik Mikael Kristensen"
    Owner:   "HMK Design"
    Rights:  "Copyright (C) HMK Design 2005"
]

ean13: context [
	; generate EAN-13 barcodes
	; highly unoptimized prototype version

	codes-left-odd: copy [
	  "0001101" "0011001" "0010011" "0111101"
	  "0100011" "0110001" "0101111" "0111011"
	  "0110111" "0001011"
	]

	codes-left-even: copy [
	  "0100111" "0110011" "0011011" "0100001"
	  "0011101" "0111001" "0000101" "0010001"
	  "0001001" "0010111"
	]

	; right codes are left odd codes reversed, so the right output is simply inverted
	; in the ean13-image function

	parity: copy [
	  "111111" "110100" "110010" "110001"
	  "101100" "100110" "100011" "101010"
	  "101001" "100101"
	]

	ean13-checksum: func [
	  "Calculates EAN13 checksum for a 12-digit input code."
	  number [string!]
	][
	  x: y: 0
	  reverse number
	  forall number [
		g: to-integer to-string number/1
		either odd? index? number [x: x + g][y: y + g]
	  ]
	  number: head number
	  reverse number
	  z: 3 * x + y
	  10 * (1 + to-integer z / 10) - z // 10
	]

	create: func [
	  "Any 12-digit code as input. Outputs valid 13-digit EAN13 code."
	  number [string!]
	][join number ean13-checksum number]

	ean13-check: func [
	  {Tests an EAN13 code to see if it has a valid checksum. Returns TRUE if correct.}
	  number [string!]
	][
	  either equal? ean13-checksum number last number [true][false]
	]

	to-int: func[input][to-integer to-string input]

	make-image: func [
	  "Outputs a bitmapped EAN-13 barcode as image!"
	  input [string!] "Must be valid 13 digit EAN-13 barcode"
	  /text {Adds number below barcode}
	  /big "Prints 2 times larger barcode."
	  /bigger {Prints 4 times larger barcode.
			   Combine with /big for 8 times larger barcode.}
	  /local t
	][
	  barsize: 1x50
	  guardbarsize: 1x60
	  spacesize: 1x0
	  fontsize: 11
	  pos0: 10x0
	  pos1: 0x45
	  pos2: 15x48
	  pos3: 60x48
	  guard: copy [
		box guardbarsize black
		box guardbarsize
		box guardbarsize black
	  ]
	  if big [
		barsize: barsize * 2
		guardbarsize: guardbarsize * 2
		spacesize: spacesize * 2
		fontsize: fontsize * 2
		pos0: pos0 * 2
		pos1: pos1 * 2
		pos2: pos2 * 2
		pos3: pos3 * 2
	  ]
	  if bigger [
		barsize: barsize * 4
		guardbarsize: guardbarsize * 4
		spacesize: spacesize * 4
		fontsize: fontsize * 4
		pos0: pos0 * 4
		pos1: pos1 * 4
		pos2: pos2 * 4
		pos3: pos3 * 4
	  ]
	  either 13 == length? input [
		t: copy compose/deep [
		  style eanlabel label font [size: (fontsize) shadow: none color: black]
		  backdrop white space 0 origin 0 across
		]

		if text [append t compose [at (pos0)]]

		; left guard
		append t guard

		; left half
		parityidx: first skip parity to-int first input
		for i 2 7 1 [ ; digit
		  digit: to-int input/:i
		  ; read parityidx for this digit
		  code: either odd? first at parityidx (i - 1) [
			first skip codes-left-odd digit
		  ][
			first skip codes-left-even digit
		  ] (i - 1)
		  for j 1 7 1 [
			append t either zero? to-int code/:j [[box barsize]][[box barsize black]]
		  ]
		]

		; center guard
		append t [box guardbarsize] 
		append t guard 
		append t [box guardbarsize]

		; right half
		for i 8 13 1 [ ; digit
		  idx: 1 + to-int input/:i
		  foreach code codes-left-odd/:idx [
			append t either zero? to-int code [[box barsize black]][[box barsize]]
		  ]
		]

		; right guard
		append t guard

		; add text to bar code
		if text [
		  append t compose [
			at (pos1)
			eanlabel (copy/part input 1)
			at (pos2)
			eanlabel (copy/part next input 6)
			at (pos3)
			eanlabel (copy/part at input 8 6)
		  ]
		]

		; generate image
		return to-image layout t
	  ][
		to-image layout/tight [
		  box 110x60 edge [size: 1x1 color: black] "Invalid barcode" font [
			size: 14 color: black shadow: none]
		]
	  ]
	]

	make-pdf: func [
	  {Outputs a block of EAN-13 barcode as vector to be inserted into a PDF document,
	   generated by pdf-maker.r}
	  input [string!] "Must be valid 13 digit EAN-13 barcode"
	  origin [block!] "Origin point for left lower corner of the bar code image"
	  /text {Adds number below barcode}
	  /local t
	][
	  scale: 0.4
	  t: copy compose [line dash solid width (scale)]
	  barsize: 1x50
	  guardbarsize: 1x60
	  spacesize: 1x0
	  fontsize: 11
	  boxes: copy []
	  pos0: [10 0]
	  pos1: [0 45]
	  pos2: [15 48]
	  pos3: [60 48]
	  startx: origin/1 / scale
	  starty: origin/2 / scale
	  guard: func [/local h] [
		h: copy compose [
		  line (startx * scale) ((starty + guardbarsize/y) * scale) (startx * scale) (starty * scale)
		  line ((startx + 2) * scale) ((starty + guardbarsize/y) * scale) ((startx + 2) * scale) (starty * scale)
		]
		startx: startx + 2
		h
	  ]
	  either 13 == length? input [
		; left guard
		append t guard
		append boxes startx

		; left half
		parityidx: first skip parity to-int first input
		for i 2 7 1 [ ; digit
		  digit: to-int input/:i
		  ; read parityidx for this digit
		  code: either odd? first at parityidx (i - 1) [
			first skip codes-left-odd digit
		  ][
			first skip codes-left-even digit
		  ] (i - 1)
		  for j 1 7 1 [
			startx: startx + 1
			if not zero? to-int code/:j [
			  append t compose/deep [
				line (startx * scale) ((starty + guardbarsize/y) * scale) (startx * scale) ((starty + guardbarsize/y - barsize/y) * scale)
			  ]
			]
		  ]
		]
		startx: startx + 2
		append boxes startx
		; center guard
		append t guard
		startx: startx + 1
		append boxes startx
		; right half
		for i 8 13 1 [ ; digit
		  idx: 1 + to-int input/:i
		  foreach code codes-left-odd/:idx [
			startx: startx + 1
			if zero? to-int code [
			  append t compose/deep [
				line (startx * scale) ((starty + guardbarsize/y) * scale) (startx * scale) ((starty + guardbarsize/y - barsize/y) * scale)
			  ]
			]
		  ]
		]

		startx: startx + 1
		append boxes startx
		; right guard
		append t guard
		append boxes startx

		; add text to bar code
		if text [
		  append t compose/deep [
			textbox (origin/1 - 7) (starty * scale) 10 ((guardbarsize/y - barsize/y + 2) * scale) [center font Helvetica (10 * scale) (copy/part input 1)]
			textbox (boxes/1 * scale) (starty * scale) ((boxes/2 - boxes/1) * scale) ((guardbarsize/y - barsize/y + 2) * scale) [center font Helvetica (10 * scale) (copy/part next input 6)]
			textbox (boxes/3 * scale) (starty * scale) ((boxes/4 - boxes/3) * scale) ((guardbarsize/y - barsize/y + 2) * scale) [center font Helvetica (10 * scale) (copy/part at input 8 6)]
		  ]
		]

		; generate image
		return t
	  ][
		make error! "Invalid code string! Not 13 digits."
	  ]
	]
]